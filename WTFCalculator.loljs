HAI

OBTW 
THIS IZ KICKASS RPN CALCULATOR IN LOLCODE
NO REAL CHECKZ FOR EDGE CASES BUT ITZ OK LOL
WORKZ ON http://asgaard.co.uk/misc/loljs KINDA

BECUZ LOL HAZ ONLY LOCAL SCOPE AN NO OBJS
STACK STORZ HEAD INDEX IN FRIST CELL
AN DATA AFTR THAT
TLDR

HOW DUZ I INIT YR STACK
	STACK!0 R GOT 0
IF U SAY SO

HOW DUZ I PUSH YR NUM AN YR STACK 
	I HAS A HEAD ITZ GOT STACK!0 
	HEAD IS NOW A NUMBAR
	HEAD R GOT SUM OF HEAD AN 1
	STACK!HEAD R GOT NUM
	STACK!0 R GOT HEAD
IF U SAY SO


HOW DUZ I POP YR STACK
        I HAS A HEAD ITZ GOT STACK!0
		HEAD IS NOW A NUMBAR
		HEAD BIGGR THAN 0, O RLY? 
		YA RLY
			I HAS A VAL ITZ GOT STACK!HEAD
			VAL IS NOW A NUMBR
			HEAD R DIFF OF HEAD AN 1
            STACK!0 R GOT HEAD			
			FOUND YR VAL	
		NO WAI
            VISIBLE "OMG STACK EMPTY! WTF"
	OIC
IF U SAY SO


BTW LETZ GET STARTED YO
I HAS A STACK ITZ GOT NOTHING
INIT STACK

BTW NEED DONE FLAG TO EXIT LOOP BECUZ GTFO IS REUSED
I HAS A DONE ITZ FAIL

IM IN YR LOOP 
	GIMMEH INPUT
	VISIBLE SMOOSH "#" INPUT
	INPUT, WTF?
		OMG "q"
			BTW GOTTA GET OUTA HERE, SET DONE FLAG CUZ GTFO IS FOR "WTF?" STMT    
			DONE R GOT WIN
			GTFO 
		OMG "+"
			I HAS A RIGHT ITZ POP STACK 
			I HAS A LEFT ITZ POP STACK
			I HAS A RSLT ITZ SUM OF LEFT AN RIGHT
			VISIBLE RSLT
			PUSH RSLT STACK
			GTFO
		OMG "-"
			I HAS A RIGHT ITZ POP STACK 
			I HAS A LEFT ITZ POP STACK
			I HAS A RSLT ITZ DIFF OF LEFT AN RIGHT
			VISIBLE RSLT
			PUSH RSLT STACK
			GTFO
		OMG "*"
			I HAS A RIGHT ITZ POP STACK 
			I HAS A LEFT ITZ POP STACK
			I HAS A RSLT ITZ PRODUKT OF LEFT AN RIGHT
			VISIBLE RSLT
			PUSH RSLT STACK
			GTFO	
		OMG "/"
			I HAS A RIGHT ITZ POP STACK 
			I HAS A LEFT ITZ POP STACK
			I HAS A RSLT ITZ QUOSHUNT OF LEFT AN RIGHT
			VISIBLE RSLT
			PUSH RSLT STACK
			GTFO				
		OMGWTF			
			I HAS A NUM
			NUM R MAEK INPUT A NUMBAR
			PUSH NUM STACK
			VISIBLE NUM
			GTFO
	OIC

	DONE, O RLY?, YA RLY
		VISIBLE "KTHXBYE"
		GTFO
	OIC
KTHX

KTHXBYE